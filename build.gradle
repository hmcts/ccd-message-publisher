plugins {
  id 'application'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.10.RELEASE'
  id 'org.springframework.boot' version '2.4.4'
  id 'org.owasp.dependencycheck' version '6.5.3'
  id 'com.github.ben-manes.versions' version '0.36.0'
  id 'org.sonarqube' version '3.0'
  id 'uk.gov.hmcts.java' version '0.11.0'
  id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.23'
}

ext['spring-framework.version'] = '5.3.20'
ext['spring-security.version'] = '5.4.5'
ext['log4j2.version'] = '2.17.1'
ext['jackson.version'] = '2.13.2'

allprojects {
  sourceCompatibility = '11'
  targetCompatibility = '11'
}

configurations {
  providedRuntime
  annotationProcessor
  aatCompile.extendsFrom(testCompile)
  aatRuntime.extendsFrom(testRuntime)
  cucumberRuntime.extendsFrom(functionalRuntime)
}

sourceSets {
  aat {
    java {
      srcDir('src/aat/java')
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
    resources {
      srcDir('src/aat/resources')
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  generateCucumberReports.enabled = false
  failFast = true
}

task functional(type: Test) {
  description = 'Executes functional tests against an the CCD Message Publisher API instance just deployed'
  dependsOn aatClasses
  new File("$buildDir/test-results/test").mkdirs()
  copy {
    from "src/aat/resources/DummyTest.xml"
    into "$buildDir/test-results/test"
  }

  doFirst {
    generateCucumberReports.enabled = true
    javaexec {
      main = "uk.gov.hmcts.ccd.messagepublisher.befta.MessagePublisherBeftaMain"
      classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
      args = [
        '--threads', '10',
        '--plugin', "json:${rootDir}/target/cucumber.json",
        '--tags', 'not @Ignore',
        '--glue', 'uk.gov.hmcts.befta.player',
        '--glue', 'uk.gov.hmcts.ccd.messagepublisher.befta', 'src/aat/resources/features'
      ]
      jvmArgs = [ '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED' ]
    }

  }

  finalizedBy {
    generateCucumberReports {
      doLast{
        delete "${rootDir}/BEFTA Report for Functional Tests/"
        new File("${rootDir}/BEFTA Report for Functional Tests").mkdirs()
        file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Functional Tests"))
        logger.quiet("Functional test report moved to ---> file://${rootDir}/BEFTA%20Report%20for%20Functional%20Tests/overview-features.html")
      }
    }
  }
  outputs.upToDateWhen { false }
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
  shouldRunAfter test
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
  // The line below prevents the build from failing if the Fortify scan detects issues
  ignoreExitValue = true
}

task smoke(type: Test) {
  description = 'Executes smoke tests against an the CCD Data Store API instance just deployed'
  dependsOn aatClasses

  new File("$buildDir/test-results/test").mkdirs()
  copy {
    from "src/aat/resources/DummyTest.xml"
    into "$buildDir/test-results/test"
  }

  doLast {
    generateCucumberReports.enabled = true
    javaexec {
      main = "uk.gov.hmcts.ccd.messagepublisher.befta.MessagePublisherBeftaMain"
      classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath
      args = ['--plugin', "json:${rootDir}/target/cucumber.json", '--tags', '@Smoke and not @Ignore', '--glue',
              'uk.gov.hmcts.befta.player', '--glue', "uk.gov.hmcts.ccd.messagepublisher.befta", 'src/aat/resources/features']
      jvmArgs = [ '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED' ]
    }
  }

  finalizedBy {
    generateCucumberReports {
      doLast{
        delete "${rootDir}/BEFTA Report for Smoke Tests/"
        new File("${rootDir}/BEFTA Report for Smoke Tests").mkdirs()
        file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Smoke Tests"))
        logger.quiet("Smoke test report moved to ---> file://${rootDir}/BEFTA%20Report%20for%20Smoke%20Tests/overview-features.html")
      }
    }
  }

  outputs.upToDateWhen { false }
}

check.dependsOn integration

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: ccd-message-publisher"
    property "sonar.projectKey", "uk.gov.hmcts.reform:ccd-message-publisher"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:2020.0.1"
  }
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.69'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '30.0-jre') {
      entry 'guava'
    }
  }
}

cucumberReports {
  outputDir = file("${projectDir}/target/cucumber")
  reports = files("${projectDir}/target/cucumber.json")
  notFailingStatuses = ["skipped", "passed"]
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://jitpack.io"
  }
}

def versions = [
  junit           : '5.7.0',
  junitPlatform   : '1.7.0',
  reformLogging   : '5.1.9',
  junitVintageVersion : '5.7.0',
  springBoot      : springBoot.class.package.implementationVersion,
  springfoxSwagger: '3.0.0',
  testcontainers  : '1.15.1',
  netty           : '4.1.77.Final'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}",
    "org.junit.vintage:junit-vintage-engine:${versions.junitVintageVersion}"
  ]
]

dependencies {
  compile "com.fasterxml.jackson.core:jackson-databind:2.13.2.2"
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.13.2'
  compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: '2.13.2'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.0.1'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-activemq'

  implementation (group: 'com.microsoft.azure', name: 'azure-servicebus-jms-spring-boot-starter', version: '2.3.5'){
    exclude group: 'org.simpleframework', module: 'simple-xml' //CVE-2017-1000190
    exclude group: 'org.nanohttpd', module: 'nanohttpd' //CVE-2020-13697
  }
  //CVE-2021-22119
  implementation (group: 'org.springframework.security', name:'spring-security-crypto', 'version':'5.4.7')
  // CVE-2021-43797
  implementation group: 'io.netty', name:'netty-buffer', version: versions.netty
  implementation group: 'io.netty', name:'netty-codec', version: versions.netty
  implementation group: 'io.netty', name:'netty-codec-http', version: versions.netty
  implementation group: 'io.netty', name:'netty-common', version: versions.netty
  implementation group: 'io.netty', name:'netty-handler', version: versions.netty
  implementation group: 'io.netty', name:'netty-resolver', version: versions.netty
  implementation group: 'io.netty', name:'netty-transport', version: versions.netty
  implementation group: 'io.netty', name:'netty-transport-native-epoll', version: versions.netty
  implementation group: 'io.netty', name:'netty-transport-native-kqueue', version: versions.netty
  implementation group: 'io.netty', name:'netty-transport-native-unix-common', version: versions.netty

  implementation group: 'org.glassfish', name: 'jakarta.el', version: '4.0.1' // CVE-2021-28170
  // CVE-2021-42550
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.10'
  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.10'

  implementation group: 'io.hawt', name: 'hawtio-springboot', version: '2.12.0'

  implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.reformLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.reformLogging

  implementation group: 'com.vladmihalcea', name: 'hibernate-types-52', version: '2.10.0'

  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  implementation group: 'org.hibernate', name: 'hibernate-core', version: '5.4.25.Final' // For CVE-2020-25638
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13' // For CVE-2020-13956
  implementation group: 'org.apache.activemq', name: 'activemq-client', version: '5.16.1' // For CVE-2021-26117
  implementation group: 'org.apache.activemq', name: 'activemq-broker', version: '5.16.1' // For CVE-2021-26117
  implementation group: 'net.minidev', name: 'json-smart', version: '2.4.7'
  implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'

  compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.0.63'
  compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.0.63'

  runtime group: 'org.postgresql', name: 'postgresql', version: '42.3.5'
  runtime group: 'com.zaxxer', name: 'HikariCP', version: '4.0.2'

  aatImplementation group: 'com.github.hmcts', name: 'ccd-test-definitions', version: '7.14.0'
  aatImplementation group: 'com.github.hmcts', name: 'befta-fw', version: '8.7.1'
  aatImplementation group: 'org.apache.poi', name: 'poi-ooxml-schemas', version: '4.1.2' //For CVE-2019-12415
  aatImplementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
  aatImplementation libraries.junit5

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
  integrationTestImplementation group: 'org.testcontainers', name: 'testcontainers', version: versions.testcontainers
  integrationTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testcontainers
  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: versions.testcontainers

  smokeTestImplementation sourceSets.main.runtimeClasspath
  testCompile 'com.github.hmcts:fortify-client:1.2.0:all'
}

mainClassName = 'uk.gov.hmcts.ccd.MessagePublisherApplication'

run {
  args = ["--spring.profiles.active=dev"]
}

bootJar {
  getArchiveFileName().set(provider {
    'ccd-message-publisher.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
