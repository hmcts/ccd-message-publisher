plugins {
  id 'application'
  id 'java'
  id 'jacoco'
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.25'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.owasp.dependencycheck' version '11.1.1'
  id 'org.sonarqube' version '6.0.1.5171'
  id 'org.springframework.boot' version '3.4.1'
  id 'uk.gov.hmcts.java' version '0.12.63'
}

ext {
  set('springCloudVersion', '2024.0.0')
  set('spring-framework.version', '6.2.1')
  set('spring-security.version', '6.4.2')
  set('log4j2.version', '2.24.3')
  set('jackson.version', '2.18.2')
  set('snakeyaml.version', '2.3')
  appInsightsVersion  = '2.6.4'
  junit               = '5.11.4'
  junitPlatform       = '1.11.4'
  lombok              = '1.18.36'
  netty               = '4.1.114.Final'
  reformLogging       = '6.1.7'
  testcontainers      = '1.20.4'
  tomcatVersion       = '10.1.34'
}

configurations {
  providedRuntime
  annotationProcessor
  aatCompile.extendsFrom(testCompile)
  aatRuntime.extendsFrom(testRuntime)
  cucumberRuntime.extendsFrom(functionalRuntime)
}

application {
  mainClass = 'uk.gov.hmcts.ccd.MessagePublisherApplication'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

sourceSets {
  aat {
    java {
      srcDir('src/aat/java')
      compileClasspath += main.output
      runtimeClasspath += main.output
    }
    resources {
      srcDir('src/aat/resources')
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(Copy).configureEach {
  duplicatesStrategy = 'exclude'
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  generateCucumberReports.enabled = false
  failFast = true
}

task functional(type: Test) {
  description = 'Executes functional tests against an the CCD Message Publisher API instance just deployed'
  dependsOn aatClasses
  copy {
    from "src/aat/resources/DummyTest.xml"
    into "${buildDir}/test-results/test"
  }

  doFirst {
    generateCucumberReports.enabled = true
    javaexec {
      main = "uk.gov.hmcts.ccd.messagepublisher.befta.MessagePublisherBeftaMain"
      classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
      args = [
        '--threads', '10',
        '--plugin', "json:${rootDir}/target/cucumber.json",
        '--tags', 'not @Ignore',
        '--glue', 'uk.gov.hmcts.befta.player',
        '--glue', 'uk.gov.hmcts.ccd.messagepublisher.befta', 'src/aat/resources/features'
      ]
      jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
    }

  }

  finalizedBy {
    generateCucumberReports {
      doLast {
        delete "${rootDir}/BEFTA Report for Functional Tests/"
        new File("${rootDir}/BEFTA Report for Functional Tests").mkdirs()
        file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Functional Tests"))
        logger.quiet("Functional test report moved to ---> file://${rootDir}/BEFTA%20Report%20for%20Functional%20Tests/overview-features.html")
      }
    }
  }
  outputs.upToDateWhen { false }
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
  shouldRunAfter test
}

task fortifyScan(type: JavaExec) {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

task smoke(type: Test) {
  description = 'Executes smoke tests against an the CCD Data Store API instance just deployed'
  dependsOn aatClasses

  copy {
    from "src/aat/resources/DummyTest.xml"
    into "${buildDir}/test-results/test"
  }

  doLast {
    generateCucumberReports.enabled = true
    javaexec {
      main = "uk.gov.hmcts.ccd.messagepublisher.befta.MessagePublisherBeftaMain"
      classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath
      args = ['--plugin', "json:${rootDir}/target/cucumber.json", '--tags', '@Smoke and not @Ignore', '--glue',
              'uk.gov.hmcts.befta.player', '--glue', "uk.gov.hmcts.ccd.messagepublisher.befta", 'src/aat/resources/features']
      jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
    }
  }

  finalizedBy {
    generateCucumberReports {
      doLast {
        delete "${rootDir}/BEFTA Report for Smoke Tests/"
        new File("${rootDir}/BEFTA Report for Smoke Tests").mkdirs()
        file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Smoke Tests"))
        logger.quiet("Smoke test report moved to ---> file://${rootDir}/BEFTA%20Report%20for%20Smoke%20Tests/overview-features.html")
      }
    }
  }

  outputs.upToDateWhen { false }
}

check.dependsOn integration

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: ccd-message-publisher"
    property "sonar.projectKey", "uk.gov.hmcts.reform:ccd-message-publisher"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation.get().asFile.path}"
  }
}

configurations.all {
  exclude group:"io.netty", module: "netty-transport-classes-epoll"
  exclude group:"io.netty", module:"netty-transport-classes-kqueue"
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
apply from: './gradle/suppress.gradle'
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

cucumberReports {
  outputDir = file("${projectDir}/target/cucumber")
  reports = files("${projectDir}/target/cucumber.json")
  notFailingStatuses = ["skipped", "passed"]
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = "https://jitpack.io"
  }
}

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${junit}",
    "org.junit.jupiter:junit-jupiter-engine:${junit}",
    "org.junit.jupiter:junit-jupiter-params:${junit}",
    "org.junit.platform:junit-platform-commons:${junitPlatform}",
    "org.junit.platform:junit-platform-engine:${junitPlatform}",
    "org.apiguardian:apiguardian-api:1.1.2"
  ]
]

dependencies {

  // start::CVE Vulnerability dependency overrides                                                        // MAIN PARENT DEPENDEDNCY

  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.16'                      // applicationinsights-logging-logback
  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.5.16'                         // applicationinsights-logging-logback
  implementation group: 'jakarta.jms', name: 'jakarta.jms-api', version: '3.1.0'                          // spring-cloud-azure-starter-servicebus-jms
  implementation group: 'commons-io', name: 'commons-io', version: '2.18.0'                               // hawtio-springboot
  implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'                              // springdoc-openapi-starter-webmvc-ui
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'                            // springdoc-openapi-starter-webmvc-ui
  implementation group: 'net.minidev', name: 'json-smart', version: '2.5.1'                               // spring-cloud-azure-starter-servicebus-jms
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: tomcatVersion      // spring-boot-starter-tomcat
  implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: tomcatVersion // spring-boot-starter-tomcat
  implementation group: 'org.hibernate', name: 'hibernate-core', version: '6.6.4.Final'                   // spring-boot-starter-data-jpa

  runtimeOnly group: 'com.zaxxer', name: 'HikariCP', version: '6.2.1'                                     // spring-boot-starter-data-jpa

  // end::CVE Vulnerability dependency overrides                                                          // MAIN PARENT DEPENDEDNCY

  // Spring
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-activemq', version: '3.4.2'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap'
  implementation group: 'org.springframework.security', name: 'spring-security-rsa', version: '1.1.5'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.6.0'

  // Netty
  implementation group: 'io.netty', name: 'netty-buffer', version: netty
  implementation group: 'io.netty', name: 'netty-codec', version: netty
  implementation group: 'io.netty', name: 'netty-codec-http', version: netty
  implementation group: 'io.netty', name: 'netty-common', version: netty
  implementation group: 'io.netty', name: 'netty-handler', version: netty
  implementation group: 'io.netty', name: 'netty-resolver', version: netty
  implementation group: 'io.netty', name: 'netty-transport', version: netty
  implementation group: 'io.netty', name: 'netty-transport-native-epoll', version: netty
  implementation group: 'io.netty', name: 'netty-transport-native-kqueue', version: netty
  implementation group: 'io.netty', name: 'netty-transport-native-unix-common', version: netty


  implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'
  implementation group: 'com.azure.spring', name: 'spring-cloud-azure-starter-servicebus-jms', version: '5.19.0'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLogging
  implementation group: 'com.microsoft.azure', name: 'applicationinsights-logging-logback', version: appInsightsVersion
  implementation group: 'com.microsoft.azure', name: 'applicationinsights-spring-boot-starter', version: appInsightsVersion
  implementation group: 'com.vladmihalcea', name: 'hibernate-types-60', version: '2.21.1'
  implementation group: 'io.hawt', name: 'hawtio-springboot', version: '4.2.0'
  implementation group: 'jakarta.el', name: 'jakarta.el-api', version: '6.0.1'
  implementation group: 'org.apache.activemq', name: 'activemq-client', version: '6.1.5'
  implementation group: 'org.apache.activemq', name: 'activemq-broker', version: '6.1.5'
  implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.4.1'
  implementation group: 'org.projectlombok', name: 'lombok', version: lombok

  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok
  runtimeOnly group: 'org.postgresql', name: 'postgresql', version: '42.7.5'


  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.6'
  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
  integrationTestImplementation group: 'org.testcontainers', name: 'testcontainers', version: testcontainers
  integrationTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: testcontainers
  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: testcontainers

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath

  aatImplementation sourceSets.main.runtimeClasspath
  aatImplementation sourceSets.test.runtimeClasspath
  aatImplementation group: 'com.github.hmcts', name: 'ccd-test-definitions', version: '7.24.2'
  aatImplementation group: 'com.github.hmcts', name: 'befta-fw', version: '9.2.1'

}

bootJar {
  archiveBaseName = 'ccd-message-publisher'
  manifest {

    attributes 'Implementation-Title': project.name,
      'Implementation-Version': project.version
  }
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
